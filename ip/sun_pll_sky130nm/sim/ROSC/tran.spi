* ROSC testbench
**********************************************************************
**        Copyright (c) 2022 Carsten Wulff Software, Norway
** *******************************************************************
** Created       : wulff at 2022-7-27
** *******************************************************************
**  The MIT License (MIT)
**
**  Permission is hereby granted, free of charge, to any person obtaining a copy
**  of this software and associated documentation files (the "Software"), to deal
**  in the Software without restriction, including without limitation the rights
**  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
**  copies of the Software, and to permit persons to whom the Software is
**  furnished to do so, subject to the following conditions:
**
**  The above copyright notice and this permission notice shall be included in all
**  copies or substantial portions of the Software.
**
**  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
**  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
**  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
**  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
**  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
**  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
**  SOFTWARE.
**
**********************************************************************


*----------------------------------------------------------------
* Include
*----------------------------------------------------------------

#ifdef Lay
.include ../../../work/lpe/SUN_PLL_ROSC_lpe.spi
#else
.include ../../../design/SUN_PLL_SKY130NM.spice
#endif



*----------------------------------------------------------------
* Options
*----------------------------------------------------------------
#ifdef Debug
.option reltol=1e-4 srcsteps=1 ramptime=10n noopiter keepopinfo gmin=1e-12
#else
.option reltol=1e-5 srcsteps=1 ramptime=10n noopiter keepopinfo gmin=1e-15
#endif

*----------------------------------------------------------------
* PARAMETERS
*----------------------------------------------------------------
.param TRF = 10p

*- Analog parameters
.param AVDD = {vdda}

*----------------------------------------------------------------
* Sources
*----------------------------------------------------------------
VSS  AVSS 0 dc 0
VDD  VDD_1V8 AVSS pwl 0 0 10n {AVDD}
VROSC VDD_ROSC AVSS dc {AVDD}
VPWR PWRUP_1V8 AVSS dc {AVDD}

*----------------------------------------------------------------
* DUT
*----------------------------------------------------------------

XDUT PWRUP_1V8 VDD_ROSC AVSS VDD_1V8 CK SUN_PLL_ROSC

*----------------------------------------------------------------
* Measures
*----------------------------------------------------------------



*----------------------------------------------------------------
* NGSPICE control
*----------------------------------------------------------------

#ifdef Debug
.probe v(CK) v(xdut.N_1) v(xdut.CKUP) v(xdut.CKDWN) v(xdut .*)
#else
.probe v(CK) v(VDD_ROSC)
#endif


.control
set num_threads=8
set color0=white
set color1=black
unset askquit

#ifdef Debug
tran 10p 50n 1p

hardcopy {cicfname}.ps allv
*quit
#else
*nbpt=64 => 8u
*tran 1n 8.5u
*npbt=128 => 16u

let start_v = 1.2
let stop_v = 1.7
let delta_v = 0.1
let v_act = start_v
* loop
while v_act le stop_v
alter VROSC v_act
tran 1p 40n
meas tran vrosc avg v(VDD_ROSC)
meas tran tpd trig v(CK) val='0.8' rise=10 targ v(CK) val='0.8' rise=11
let v_act = v_act + delta_v
end


quit
#endif

.endc

.end
